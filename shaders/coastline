vs .1 .1

;	v0			= position
;	v1			= index
;	v2			= normal
;	v3			= texture coordinates

;	c0			= constants [0, 1, 0, 255.01]
;	c1 - c4		= world-view-projection matrix
;	c5			= colour
;	c6-c95		= indexed data

;	r0			= index intermediate
;	r1			= indexed data cache (x = normal multiplier)
;	r2			= world position

; compensate for lack of UBYTE4 on Geforce3

mul		r0,		v1.zyxw,	c0.wwww
; mul	r0,		v1,			c0.wwww		- the way if have ubyte4

mov		a0.x,	r0.x					; put index into address register
mov		r1,		c[a0.x + 6]				; move index data into register

mul		r2,		v2,			r1.x		; multiply normal by offset
add		r2,		r2,			v0			; add in world position

mov		r2.w,	c0.y					; set world position w component to 1		

m4x4	oPos,	r2,			c1			; transform the world position into homogenous clip space and output

mov		oD0,	c5						; pass through the colour

mov  	oT0, 	v3						; pass through the texture coordinates
	

